import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        springBootVersion = '2.6.3'
    }
}

plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version "${springBootVersion}"
}

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    configurations {
        all {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
        mavenLocal()
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/jfrossetto/spring-libs"
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    dependencies {

        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        runtimeOnly 'com.vlkan.log4j2:log4j2-logstash-layout:1.0.2'

        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'io.projectreactor:reactor-test'

        // Lombok
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    group 'br.com.jfr.homebudget'
    version '0.0.1'

    sourceCompatibility = '11'
    targetCompatibility = '11'

    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            applyMavenExclusions true
        }
    }

}
